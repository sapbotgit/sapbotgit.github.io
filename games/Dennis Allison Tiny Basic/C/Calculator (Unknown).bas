10 REM "Pocket calculator in BASIC"
20 REM ""
100 REM "Definitions - keywords and tokens"
110 DATA "+", 2, "-", 3, "*", 4, "/", 5, "p", 6, "d", 7, "q", 8
111 DATA "sqr", 9, "dup", 10, "swap", 11, "", 12, "", 13, "", 14, "", 15, "", 16
120 REM "The stack"
130 S=0
140 N=16
150 DIM S(N)
160 REM "Errors"
170 E=0
200 REM "Input loop"
210 PRINT "<";S;">:"; :INPUT A$
220 @S=0
230 A=VAL(A$)
240 IF @S=0 THEN T=1 : GOTO 500
300 T=-1
310 RESTORE
330 @S=0
340 FOR I
350 READ K$
360 READ X
370 IF K$=A$ THEN T=X : BREAK
380 IF @S<>0 THEN BREAK
390 NEXT 
400 IF T=-1 THEN E=1
500 REM "The token switch, with an evil expression GOSUB"
505 REM "Line numbers 1000, 1100, ... are targeted"
510 IF T>0 GOSUB 900+100*T
600 IF E<>0 THEN PRINT "Error" : E=0 : GOTO 200
610 IF S>0 THEN PRINT S(S)
620 GOTO 200
1000 REM "Enter a number"
1010 IF S=N THEN E=0: RETURN 
1020 S=S+1 : S(S)=A
1030 RETURN
1100 REM "Addition"
1110 IF S<2 THEN E=1: RETURN
1120 S(S-1)=S(S-1)+S(S)
1130 S=S-1
1140 RETURN
1200 REM "Subtraction"
1210 IF S<2 THEN E=1: RETURN
1220 S(S-1)=S(S-1)-S(S)
1230 S=S-1
1240 RETURN
1300 REM "Multiplication"
1310 IF S<2 THEN E=1: RETURN
1320 S(S-1)=S(S-1)*S(S)
1330 S=S-1
1340 RETURN
1400 REM "Division"
1410 IF S<2 OR S(S)=0 THEN E=1: RETURN
1420 S(S-1)=S(S-1)/S(S)
1430 S=S-1
1440 RETURN
1500 REM "Display a number"
1510 IF S=0 THEN E=1
1530 RETURN
1600 REM "Drop"
1610 IF S=0 THEN E=1 ELSE S=S-1
1620 RETURN
1700 REM "Quit"
1710 END
1800 REM "Square root"
1810 IF S(S)<0 OR S=0 THEN E=1 ELSE S(S)=SQR(S(S))
1820 RETURN
1900 REM "Dup"
1910 IF S>N-1 OR S=0 THEN E=1 ELSE S=S+1: S(S)=S(S-1)
1920 RETURN
2000 REM "Swap"
2010 IF S=N OR S<2 THEN E=1 ELSE A=S(S) : S(S)=S(S-1) : S(S-1)=A
2020 RETURN
2100 REM ""
2120 RETURN
2200 REM ""
2220 RETURN
2300 REM ""
2320 RETURN
2400 REM ""
2420 RETURN
2500 REM ""
2520 RETURN


